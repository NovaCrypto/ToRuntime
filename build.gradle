/*
 *  ToRuntime library, Java promotions of checked exceptions to runtime exceptions
 *  Copyright (C) 2017 Alan Evans, NovaCrypto
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <https://www.gnu.org/licenses/>.
 *
 *  Original source: https://github.com/NovaCrypto/ToRuntime
 *  You can contact the authors via github issues.
 */

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}
apply plugin: 'maven-publish'

group 'io.github.novacrypto'
version '0.9.0'

apply plugin: 'java'
apply plugin: 'jacoco'

repositories {
    jcenter()
}

compileJava {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:3.8.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId group
            artifactId 'ToRuntime'
            version project.version
            artifact("$buildDir/libs/toruntime-${project.version}-sources.jar"){
                classifier = 'sources'
            }
            artifact("$buildDir/libs/toruntime-${project.version}-javadoc.jar"){
                classifier = 'javadoc'
            }
            artifact("$buildDir/libs/toruntime-${project.version}.jar")
        }
    }
}

// To upload a new version
// ./gradlew clean
// ./gradlew build bintrayUpload -Puser=user -Pkey=APIKEY
bintray {
    user = project.ext.properties.user
    key = project.ext.properties.key
    publications = ['MyPublication']
    pkg {
        repo = 'General'
        name = 'ToRuntime'
        userOrg = 'novacrypto'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/NovaCrypto/ToRuntime.git'
        version project.version
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport